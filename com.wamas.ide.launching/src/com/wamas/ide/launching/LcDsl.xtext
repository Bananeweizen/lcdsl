grammar com.wamas.ide.launching.LcDsl with org.eclipse.xtext.common.Terminals

generate lcDsl "http://www.wamas.com/ide/launching/LcDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

/**
 * TODO: The language is still missing some things that could be set:
 * 
 * General: variable expansion for all paths (string subst, system properties)!
 * 
 * Java:
 *   - support for setting JRE
 *   - main class search settings (system libraries, inheritence)
 *   - stop in main
 *   - additional classpath entries (low prio)
 *   - additional source lookup (low prio)
 * 
 * RAP:
 *   - servlet path, port, context path
 *   - session timeout
 *   - open internal, external, not at all
 *   - RAP development mode
 *   - JRE settings (same as Java - but different)
 * 
 * How to support different launch config types - some generic attribute support?
 * Or really implement support for each type separately (probably the best experience).
 */

LCModel:
	configurations+=LaunchConfig*
;

/**
 * explicit = explicitly defined plug-in list. no auto-resolve of plug-ins.
 * manual = manually generated through context menu only. no automatic build.
 * foreground = don't launch in background, but rather block
 * no-console = don't allocate a console (no stdin then)
 * no-validate = don't validate plugins prior to launch
 * sw-install-allowed = allow software installation in eclipse config
 * replace-env = replace environment instead of appending variables
 */
LaunchConfig:
	((explicit?='explicit')? 
		& (manual?='manual')? 
		& (foreground?='foreground')? 
		& (noConsole?='no-console')?
		& (noValidate?='no-validate')?
		& (swInstallSupport?='sw-install-allowed')?
		& (replaceEnv?='replace-env')?
	) type=LaunchConfigType 'configuration' name=FQName 
	(':' superConfig=[LaunchConfig|FQName])? '{'
		( // things that may appear only once
			(clears=ClearOption ';')?
			& ('workspace' workspace=AnyPath ';')?
			& ('working-dir' workingDir=ExistingPath ';')?
			& (memory=MemoryOption ';')?
			& ('project' project=Project ';')? // TODO: content assist
			& ('main-class' mainClass=JavaType ';')? // TODO: content assist
			& (application=ApplicationExtPoint)? // TODO: content assist, validation
			& (product=ProductExtPoint)? // TODO: content assist, validation
			& (favorites=Favorites)?
			& (redirect=Redirect)?
			& (execEnv=ExecutionEnvironment)? // TODO: content assist, validation
			& (configIniTemplate=ConfigIniTemplate)?
		)
		( // things that may appear multiple times
			plugins+=AddPlugin // TODO: content assist
			| ignore+=IgnorePlugin // TODO: content assist
			| groupMembers+=GroupMember // TODO: content assist, validation, update on rename
			| vmArgs+=VmArgument
			| progArgs+=ProgramArgument
			| envVars+=EnvironmentVariable
			| traces+=TraceEnablement // TODO: content assist, validation
		)*
	'}'
;

AddPlugin:
	'plugin' plugin=PluginWithVersionAndStartLevel ';'
;

IgnorePlugin:
	'ignore' plugin=PluginWithVersion ';'
;

VmArgument:
	{VmArgument} 'vm-arg' (arguments+=STRING)* ';'
;

ProgramArgument:
	{ProgramArgument} 'argument' (arguments+=STRING)* ';'
;

EnvironmentVariable:
	{EnvironmentVariable} 'environment' name=ID EQ value=STRING ';'
;

ApplicationExtPoint:
	'application' name = FQName ';'
;

ProductExtPoint:
	'product' name = FQName ';'
;

Favorites:
	{Favorites} 'favorite' ((run?='run')? & (debug?='debug')?) ';'
;

Redirect:
	{Redirect} 'redirect' ((outWhich = OutputStream 'to' outFile=AnyPath)? & (inWhich=InputStream 'from' inFile=ExistingPath)?) ';'
;

ExecutionEnvironment:
	'execution-environment' name=ID ';'
;

ConfigIniTemplate:
	'config-ini-template' path=ExistingPath ';'
;

TraceEnablement:
	'trace' plugin=PluginWithVersion (what+=ID)* ';'
;

Path:
	ExistingPath|AnyPath
;
ExistingPath:
	name = STRING
;
AnyPath:
	name = STRING
;

PluginWithVersionAndStartLevel:
	plugin = PluginWithVersion ((autoStart?='autostart')? & ('startlevel' startLevel=INT)?)
;

PluginWithVersion:
	Plugin (version=VERSION)?
;

Plugin:
	name = FQName
;

Project:
	name = FQName
;

JavaType:
	name = FQName
;

ClearOption:
	{ClearOption} 'clear' (((workspace?='workspace' | log?='log') (noAskClear?='!')?)? & (config?='config')?)
;

MemoryOption:
	{MemoryOption} 'memory' (('min' EQ min=INT minUnit=MemoryUnit)? & ('max' EQ max=INT maxUnit=MemoryUnit)? & ('perm' EQ perm=INT permUnit=MemoryUnit)?)
;

GroupMember:
	((type?=LaunchModeType)? & (adopt?='adopt')?) 'member' member=[LaunchConfig|FQName] (postAction?=GroupPostLaunchAction)? ';'
;

GroupPostLaunchAction:
	GroupPostLaunchDelay|GroupPostLaunchRegex|GroupPostLaunchWait
;

GroupPostLaunchDelay:
	'delay' delay=INT
;

GroupPostLaunchRegex:
	'regex' regex=STRING
;

GroupPostLaunchWait:
	{GroupPostLaunchWait} 'wait'
;

/**
 * A possibly fully qualified name (separated by '.').
 */
FQName:
	ID ('.' ID)*
;

enum LaunchConfigType:
	JAVA = "java" | ECLIPSE = "eclipse" | RAP = "rap" | GROUP = "group"
;

enum LaunchModeType:
	RUN = "run" | DEBUG = "debug" | PROFILE = "profile" | COVERAGE = "coverage" | INHERIT = "inherit"
;

enum MemoryUnit:
	MB = 'MB' | MB = 'mb' | MB = 'M' | MB = 'm' |
	GB = 'GB' | GB = 'gb' | GB = 'G' | GB = 'g' 
;

enum OutputStream:
	STDOUT = "stdout" | STDERR = "stderr" | BOTH = "both-out"
;

enum InputStream:
	STDIN = "stdin"
;

terminal INT returns ecore::EInt: ('0'..'9')+;
terminal ID: '^'?('a'..'z'|'A'..'Z'|'_') ('.'? ('a'..'z'|'A'..'Z'|'^'|'_'|'-'|'0'..'9'))*;
terminal QUALIFIER: ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;
terminal VERSION: INT (('.' INT) (('.' INT) ('.' QUALIFIER)?)?)?;
terminal EQ: '=';