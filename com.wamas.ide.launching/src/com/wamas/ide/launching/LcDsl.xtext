grammar com.wamas.ide.launching.LcDsl with org.eclipse.xtext.common.Terminals

generate lcDsl "http://www.wamas.com/ide/launching/LcDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

/**
 * TODO: The language is still missing some things that could be set:
 * 
 * General: variable expansion for all paths (string subst, system properties)!
 * 
 * Java:
 *   - additional classpath entries (low prio)
 *   - additional source lookup (low prio)
 * 
 * How to support different launch config types - some generic attribute support?
 * Or really implement support for each type separately (probably the best experience).
 */

LCModel:
	configurations+=LaunchConfig*
;

/**
 * explicit = explicitly defined plug-in list. no auto-resolve of plug-ins.
 * manual = manually generated through context menu only. no automatic build.
 * foreground = don't launch in background, but rather block
 * no-console = don't allocate a console (no stdin then)
 * no-validate = don't validate plugins prior to launch
 * sw-install-allowed = allow software installation in eclipse config
 * replace-env = replace environment instead of appending variables
 */
LaunchConfig:
	((explicit?='explicit')? // All
		& (manual?='manual')? // All
		& (foreground?='foreground')? // All  
		& (noConsole?='no-console')? // All but Groups
		& (noValidate?='no-validate')? // Eclipse, RAP
		& (swInstallSupport?='sw-install-allowed')? // Eclipse
		& (replaceEnv?='replace-env')? // All but Groups
		& (stopInMain?='stop-in-main')? // Java
	) type=LaunchConfigType 'configuration' name=FQName 
	(':' superConfig=[LaunchConfig|FQName])? BLOCK_BEGIN
		( // things that may appear only once
			(clears=ClearOption ';')? // Eclipse, RAP (partial)
			& ('workspace' workspace=AnyPath ';')? // Eclipse, RAP
			& ('working-dir' workingDir=ExistingPath ';')? // All but Groups
			& (memory=MemoryOption ';')? // All but Groups
			& ('project' project=Project ';')? // Java 				// TODO: content assist
			& ('main-class' mainClass=JavaMainType ';')? // Java 		// TODO: content assist, validation
			& (application=ApplicationExtPoint)? // Eclipse 		// TODO: content assist, validation
			& (product=ProductExtPoint)? // Eclipse 				// TODO: content assist, validation
			& (favorites=Favorites)? // All
			& (redirect=Redirect)? // All but Groups
			& (execEnv=ExecutionEnvironment)? // All but Groups 	// TODO: content assist, validation
			& (configIniTemplate=ConfigIniTemplate)? // Eclipse
			& (javaMainSearch=JavaMainSearch)? // Java
			& (servletConfig=RapServletConfig)? // RAP
		)
		( // things that may appear multiple times
			plugins+=AddPlugin // Eclipse, RAP 						// TODO: content assist
			| ignore+=IgnorePlugin // Eclipse, RAP 					// TODO: content assist
			| groupMembers+=GroupMember // Groups 					// TODO: content assist, validation, update on rename
			| vmArgs+=VmArgument // All but Groups
			| progArgs+=ProgramArgument // All but Groups
			| envVars+=EnvironmentVariable // All but Groups
			| traces+=TraceEnablement // Eclipse, RAP 				// TODO: content assist, validation
		)*
	BLOCK_END
;

AddPlugin:
	'plugin' plugin=PluginWithVersionAndStartLevel ';'
;

IgnorePlugin:
	'ignore' plugin=PluginWithVersion ';'
;

VmArgument:
	{VmArgument} 'vm-arg' (arguments+=STRING)* ';'
;

ProgramArgument:
	{ProgramArgument} 'argument' (arguments+=STRING)* ';'
;

EnvironmentVariable:
	{EnvironmentVariable} 'environment' name=ID EQ value=STRING ';'
;

ApplicationExtPoint:
	'application' name = FQName ';'
;

ProductExtPoint:
	'product' name = FQName ';'
;

Favorites:
	{Favorites} 'favorite' ((run?='run')? & (debug?='debug')?) ';'
;

Redirect:
	{Redirect} 'redirect' ((outWhich = OutputStream 'to' outFile=AnyPath)? & (inWhich=InputStream 'from' inFile=ExistingPath)?) ';'
;

ExecutionEnvironment:
	'execution-environment' name=ID ';'
;

ConfigIniTemplate:
	'config-ini-template' path=ExistingPath ';'
;

TraceEnablement:
	'trace' plugin=PluginWithVersion (what+=ID)* ';'
;

JavaMainSearch:
	{JavaMainSearch} 'search-main' ((system?='system')? & (inherited?='inherited')?) ';'
;

RapServletConfig:
	'servlet' BLOCK_BEGIN
		(
			('path' servletPath=STRING ';')
			& ('browser' browserMode=BrowserLaunchMode ';')?
			& ('port' serverPort=INT ';')?
			& ('session-timeout' sessionTimeout=INT ';')?
			& ('context-path' contextPath=STRING ';')?
			& ('dev-mode' devMode=BOOLEAN ';')?
		)
	BLOCK_END
;

Path:
	ExistingPath|AnyPath
;
ExistingPath:
	name = STRING
;
AnyPath:
	name = STRING
;

PluginWithVersionAndStartLevel:
	plugin = PluginWithVersion ((autoStart?='autostart')? & ('startlevel' startLevel=INT)?)
;

PluginWithVersion:
	name = FQName (version=VERSION)?
;

Project:
	name = FQName
;

JavaMainType:
	name = FQName
;

ClearOption:
	{ClearOption} 'clear' (((workspace?='workspace' | log?='log') (noAskClear?='!')?)? & (config?='config')?)
;

MemoryOption:
	{MemoryOption} 'memory' (('min' EQ min=INT minUnit=MemoryUnit)? & ('max' EQ max=INT maxUnit=MemoryUnit)? & ('perm' EQ perm=INT permUnit=MemoryUnit)?)
;

GroupMember:
	((type?=LaunchModeType)? & (adopt?='adopt')?) 'member' member=[LaunchConfig|FQName] (postAction?=GroupPostLaunchAction)? ';'
;

GroupPostLaunchAction:
	GroupPostLaunchDelay|GroupPostLaunchRegex|GroupPostLaunchWait
;

GroupPostLaunchDelay:
	'delay' delay=INT
;

GroupPostLaunchRegex:
	'regex' regex=STRING
;

GroupPostLaunchWait:
	{GroupPostLaunchWait} 'wait'
;

/**
 * A possibly fully qualified name (separated by '.').
 */
FQName:
	ID ('.' ID)*
;

enum BrowserLaunchMode:
	INTERNAL='internal' | EXTERNAL = 'external' | NONE = 'none'
;

enum LaunchConfigType:
	JAVA = "java" | ECLIPSE = "eclipse" | RAP = "rap" | GROUP = "group"
;

enum LaunchModeType:
	RUN = "run" | DEBUG = "debug" | PROFILE = "profile" | COVERAGE = "coverage" | INHERIT = "inherit"
;

enum MemoryUnit:
	MB = 'MB' | MB = 'mb' | MB = 'M' | MB = 'm' |
	GB = 'GB' | GB = 'gb' | GB = 'G' | GB = 'g' 
;

enum OutputStream:
	STDOUT = "stdout" | STDERR = "stderr" | BOTH = "both-out"
;

enum InputStream:
	STDIN = "stdin"
;


terminal INT returns ecore::EInt: ('0'..'9')+;
terminal BOOLEAN returns ecore::EBoolean: ('true' | 'false');
terminal ID: '^'?('a'..'z'|'A'..'Z'|'_') ('.'? ('a'..'z'|'A'..'Z'|'^'|'_'|'-'|'0'..'9'))*;
terminal QUALIFIER: ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;
terminal VERSION: INT (('.' INT) (('.' INT) ('.' QUALIFIER)?)?)?;
terminal EQ: '=';
terminal BLOCK_BEGIN: '{';
terminal BLOCK_END: '}';